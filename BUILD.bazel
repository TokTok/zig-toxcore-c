load("@rules_zig//zig:defs.bzl", "zig_binary", "zig_library", "zig_module", "zig_test")
load("//tools/project:build_defs.bzl", "project")

project(license = "gpl3-https")

zig_module(
    name = "sodium",
    main = "src/sodium.zig",
    tags = ["no-windows"],
)

zig_module(
    name = "tox",
    srcs = [
        "src/friend.zig",
        "src/hex.zig",
        "src/wrap.zig",
    ],
    main = "src/tox.zig",
    tags = ["no-windows"],
    deps = [":sodium"],
)

zig_library(
    name = "zig-toxcore-c",
    cdeps = [
        "//c-toxcore",
        "@libsodium",
        "@rules_zig//zig/lib:libc",
    ],
    main = "src/tox.zig",
    tags = ["no-windows"],
    deps = [":tox"],
)

zig_module(
    name = "node_info",
    srcs = [
        "apps/BootNode.zig",
        "apps/Node.zig",
        "apps/NodeInfo.zig",
        "apps/QueryNode.zig",
        "apps/RespNode.zig",
    ],
    main = "src/tox.zig",
    tags = ["no-windows"],
    deps = [":sodium"],
)

[zig_binary(
    name = app,
    cdeps = [
        "//c-toxcore",
        "@libsodium",
        "@rules_zig//zig/lib:libc",
    ],
    main = "apps/%s.zig" % app,
    tags = ["no-windows"],
    deps = [
        ":node_info",
        ":sodium",
        ":tox",
    ],
) for app in [
    "key",
    "show-keys",
    "local-test",
    "echo-bot",
]]

[zig_test(
    name = src[4:-4].replace("/", "_") + "_test",
    size = "small",
    cdeps = [
        "//c-toxcore",
        "@rules_zig//zig/lib:libc",
    ],
    main = src,
    tags = ["no-windows"],
    deps = [":tox"],
) for src in glob(["src/*.zig"])]
